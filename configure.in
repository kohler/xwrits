dnl Process this file with autoconf to produce a configure script.
AC_INIT(xwrits.h)
AM_INIT_AUTOMAKE(xwrits, 2.7.2)

AC_PROG_CC
AC_C_CONST

AC_ARG_ENABLE(warn,
[  --enable-warn           compile with -Wall],
if test "$enableval" = yes ; then
  CC="$CC -Wall"
fi)

AC_PROG_INSTALL
AC_ARG_PROGRAM

AC_PATH_XTRA

dnl Don't do anything special with memory allocation for the GIF library.
GIF_MEMORY_DEFINES=''
AC_SUBST(GIF_MEMORY_DEFINES)dnl

dnl
dnl strerror()? unistd.h?
dnl

AC_CHECK_FUNC(strerror, STRERROR_O='', STRERROR_O='strerror.o')
AC_SUBST(STRERROR_O)dnl

dnl
dnl gettimeofday()
dnl

AC_CACHE_CHECK(for gettimeofday prototype, ac_cv_gettimeofday,
[AC_TRY_COMPILE([#include <time.h>
#include <sys/time.h>],
[gettimeofday((void *)0, (void *)0);],
[AC_TRY_COMPILE([#include <time.h>
#include <sys/time.h>],
[gettimeofday((void *)0);],
ac_cv_gettimeofday=0,
ac_cv_gettimeofday=2)],
ac_cv_gettimeofday=1)])
if test $ac_cv_gettimeofday = 1 ;
then AC_DEFINE_UNQUOTED(SYSV_GETTIMEOFDAY)
elif test $ac_cv_gettimeofday = 0 ;
then AC_DEFINE_UNQUOTED(NO_GETTIMEOFDAY_PROTO)
fi

dnl
dnl Integer typedefs
dnl

AC_CACHE_CHECK(for integer typedefs, ac_cv_int_types,
[AC_EGREP_HEADER(u_int32_t, sys/types.h, ac_cv_int_types=yes,
  AC_EGREP_HEADER(uint32_t, sys/types.h, ac_cv_int_types=close,
   ac_cv_int_types=no))])

if test "$ac_cv_int_types" = no ;
then
  # Define our own versions
  AC_CACHE_CHECK(for a 16-bit value, ac_cv_int16,
  [AC_TRY_RUN([#include <stdio.h>
#include <stdlib.h>
int main(int argc, char **argv) {
  FILE *f = fopen("conftestval", "w");
  if (!f) exit(1);
  if (sizeof(short) == 2) fprintf(f, "short\n");
  else exit(1);
  fclose(f);
  exit(0);
}], ac_cv_int16=`cat conftestval`,
  AC_MSG_ERROR(no 16-bit value found),
  ac_cv_int16=short)])

  AC_CACHE_CHECK(for a 32-bit value, ac_cv_int32,
  [AC_TRY_RUN([#include <stdio.h>
#include <stdlib.h>
int main(int argc, char **argv) {
  FILE *f = fopen("conftestval", "w");
  if (!f) exit(1);
  if (sizeof(int) == 4) fprintf(f, "int\n");
  else if (sizeof(long) == 4) fprintf(f, "long\n");
  else exit(1);
  fclose(f);
  exit(0);
}], ac_cv_int32=`cat conftestval`, 
  AC_MSG_ERROR(no 32-bit value found),
  ac_cv_int32=int)])
fi

if test "$ac_cv_int_types" = no ;
then
  INTEGER_TYPES="typedef unsigned $ac_cv_int16 u_int16_t;\\
typedef unsigned $ac_cv_int32 u_int32_t;\\
typedef $ac_cv_int32 int32_t;"

elif test "$ac_cv_int_types" = close ;
then
  INTEGER_TYPES="#include <sys/types.h>\\
typedef uint16_t u_int16_t;\\
typedef uint32_t u_int32_t;"

else
  INTEGER_TYPES="#include <sys/types.h>"
fi
AC_SUBST(INTEGER_TYPES)dnl

dnl
dnl Output
dnl

AC_OUTPUT(Makefile gif.h)
